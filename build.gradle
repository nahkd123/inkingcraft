plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id 'eclipse'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url "https://maven.terraformersmc.com/releases/" }

	// Include Maven local repository
	// "inking" must be installed manually.
	mavenLocal()
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"inkingcraft" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Mods
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.terraformersmc:modmenu:9.0.0"

	// Libraries
	api include('io.github.nahkd123:inking-api:0.0.1-SNAPSHOT')
	implementation include('io.github.nahkd123:inking-otd:0.0.1-SNAPSHOT')
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

tasks.withType(JavaCompile).each {
	// Enable preview features for inking-otd
	it.options.compilerArgs.add('--enable-preview')
}

loom {
	runs {
		client {
			// Enable preview features for inking-otd when testing
			// Server might not needed because this is purely client-side mod!
			vmArgs '--enable-preview'
		}
	}
}

eclipse {
	synchronizationTasks eclipseJdt

	jdt.file.withProperties { props ->
		props['org.eclipse.jdt.core.compiler.problem.enablePreviewFeatures']= 'enabled'
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}